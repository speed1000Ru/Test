
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Управленческий.Записывать=Истина;
	Движения.Управленческий.Очистить();
	Движения.Управленческий.Записать();
	
	Блокировка=Новый БлокировкаДанных;
	ЭлементБлокировки=Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Покупатели);
	Блокировка.Заблокировать();
	
	Запрос = Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	УправленческийОстатки.Субконто1 КАК Контрагент,
	             |	УправленческийОстатки.Субконто2 КАК Договор,
	             |	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
	             |	УправленческийОстатки.СуммаВалютнаяОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) - УправленческийОстатки.СуммаОстаток КАК Отклонение
	             |ИЗ
	             |	РегистрБухгалтерии.Управленческий.Остатки(&МоментИтогов, Счет = &СчетПокупатели, &Субконто, ) КАК УправленческийОстатки
	             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментИтогов, ) КАК КурсыВалютСрезПоследних
	             |		ПО УправленческийОстатки.Субконто2.Валюта = КурсыВалютСрезПоследних.Валюта
	             |ГДЕ
	             |	УправленческийОстатки.СуммаВалютнаяОстаток * ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) - УправленческийОстатки.СуммаОстаток <> 0";
	
	Субконто = Новый Массив(2);
	Субконто[0]=ПланыВидовХарактеристик.ВидыСубконто.Контрагент;
	Субконто[1]=ПланыВидовХарактеристик.ВидыСубконто.Договор;
	
	Запрос.УстановитьПараметр("МоментИтогов", МоментВремени());
	Запрос.УстановитьПараметр("СчетПокупатели", ПланыСчетов.Управленческий.Покупатели);
	Запрос.УстановитьПараметр("Субконто", Субконто);
	
	Выборка=Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Проводка = Движения.Управленческий.Добавить();
		Проводка.Период=Дата;
		
		Если Выборка.Отклонение > 0 Тогда
			Проводка.СчетДт=ПланыСчетов.Управленческий.Покупатели;
			Проводка.СчетКт=ПланыСчетов.Управленческий.ПрибылиУбытки;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Контрагент]=Выборка.Контрагент;
			Проводка.СубконтоДт[ПланыВидовХарактеристик.ВидыСубконто.Договор]=Выборка.Договор;
			Проводка.Сумма=Выборка.Отклонение;
		Иначе
			Проводка.СчетДт=ПланыСчетов.Управленческий.ПрибылиУбытки;
			Проводка.СчетКт=ПланыСчетов.Управленческий.Покупатели;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагент]=Выборка.Контрагент;
			Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Договор]=Выборка.Договор;
			Проводка.Сумма=-Выборка.Отклонение;
		КонецЕсли;
	КонецЦикла;
	
	
	
	
КонецПроцедуры
