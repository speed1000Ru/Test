
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.Управленческий.Записывать=Истина;
	Движения.Управленческий.БлокироватьДляИзменения=Истина;
	Движения.Управленческий.Очистить();
	Движения.Управленческий.Записать();
	
	Блокировка=Новый БлокировкаДанных;
	ЭлементБлокировки=Блокировка.Добавить("РегистрБухгалтерии.Управленческий");
	ЭлементБлокировки.УстановитьЗначение("Счет", ПланыСчетов.Управленческий.Покупатели);
	ЭлементБлокировки.УстановитьЗначение(ПланыВидовХарактеристик.ВидыСубконто.Контрагент, Контрагент);
	Блокировка.Заблокировать();
	
	Запрос=Новый Запрос;
	Запрос.Текст="ВЫБРАТЬ
	             |	УправленческийОстатки.Субконто2 КАК Договор,
	             |	УправленческийОстатки.СуммаОстаток КАК СуммаОстаток,
	             |	УправленческийОстатки.СуммаВалютнаяОстаток,
	             |	ЕСТЬNULL(КурсыВалютСрезПоследних.Курс, 1) КАК Курс,
	             |	УправленческийОстатки.Субконто2.Валюта КАК Валюта
	             |ИЗ
	             |	РегистрБухгалтерии.Управленческий.Остатки(&МоментИтогов, Счет = &СчетПокупатели, &Субконто, Субконто1 = &КОнтрагент) КАК УправленческийОстатки
	             |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&МоментИтогов, ) КАК КурсыВалютСрезПоследних
	             |		ПО УправленческийОстатки.Субконто2.Валюта = КурсыВалютСрезПоследних.Валюта
	             |
	             |УПОРЯДОЧИТЬ ПО
	             |	СуммаОстаток УБЫВ";
				 
	Субконто=Новый Массив(2);
	Субконто[0]=ПланыВидовХарактеристик.ВидыСубконто.Контрагент;
	Субконто[1]=ПланыВидовХарактеристик.ВидыСубконто.Договор;
	
	Запрос.УстановитьПараметр("МоментИтогов", МоментВремени());
	Запрос.УстановитьПараметр("СчетПокупатели", ПланыСчетов.Управленческий.Покупатели);
	Запрос.УстановитьПараметр("Субконто", Субконто);
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	
	Выборка=Запрос.Выполнить().Выбрать();
	ОсталосьСписать=Сумма;
	Пока Выборка.Следующий() и ОсталосьСписать<>0 Цикл
		Списываем = Мин (ОсталосьСписать, Выборка.СуммаВалютнаяОстаток * Выборка.Курс);
		Проводка=Движения.Управленческий.Добавить();
		Проводка.Период=Дата;
		Проводка.СчетДт=ПланыСчетов.Управленческий.Касса;
		Проводка.СчетКт=ПланыСчетов.Управленческий.Покупатели;
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Контрагент]=Контрагент;
		Проводка.СубконтоКт[ПланыВидовХарактеристик.ВидыСубконто.Договор]=Выборка.Договор;
		Проводка.СуммаВалютнаяКт=Списываем/Выборка.Курс;
		Проводка.Сумма=Списываем;
		ОсталосьСписать=ОсталосьСписать-Списываем;
	КонецЦикла;
	
КонецПроцедуры
