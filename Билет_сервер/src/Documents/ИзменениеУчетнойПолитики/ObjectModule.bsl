
Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	Движения.УчетнаяПолитика.Записывать=Истина;
	Движения.УчетнаяПолитика.Очистить();
	Движение=Движения.УчетнаяПолитика.Добавить();
	Движение.Период=Дата;
	Движение.МетодСписания=МетодСписания;
	
	Движения.ОстаткиНоменклатуры.Записывать=Истина;
	Движения.ОстаткиНоменклатуры.Очистить();
	
	Если МетодСписания=Перечисления.МетодыСписания.ПоСредней Тогда
		
		Движения.ОстаткиНоменклатуры.Записать();
		
		Блокировка=Новый БлокировкаДанных;
		ЭлементБлокировки=Блокировка.Добавить("РегистрНакопления.ОстаткиНоменклатуры");
		Блокировка.Заблокировать();
		
		Запрос=Новый Запрос;
		Запрос.Текст="ВЫБРАТЬ
		             |	ОстаткиНоменклатурыОстатки.Номенклатура,
		             |	ОстаткиНоменклатурыОстатки.Партия,
		             |	ОстаткиНоменклатурыОстатки.КоличествоОстаток КАК Количество,
		             |	ОстаткиНоменклатурыОстатки.СуммаОстаток КАК Сумма
		             |ИЗ
		             |	РегистрНакопления.ОстаткиНоменклатуры.Остатки(&МоментИтогов, ) КАК ОстаткиНоменклатурыОстатки";
					 
		Запрос.УстановитьПараметр("МоментИтогов", МоментВремени());
		
		Выборка = Запрос.Выполнить().Выбрать();
		Пока Выборка.Следующий() Цикл
			Движение=Движения.ОстаткиНоменклатуры.ДобавитьПриход();
			Движение.Период=Дата;
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			Движение.Количество=-Выборка.Количество;
			Движение.Сумма=-Выборка.Сумма;
			
			Движение=Движения.ОстаткиНоменклатуры.Добавить();
			Движение.Период=Дата;
			ЗаполнитьЗначенияСвойств(Движение, Выборка);
			Движение.Партия=Ссылка;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры
